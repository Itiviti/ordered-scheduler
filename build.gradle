plugins {
    id 'com.jfrog.bintray' version '1.1'
    id 'com.ullink.functions' version '1.0'
    id 'com.ullink.task-rules' version '1.0'
}

apply plugin: 'java'
apply plugin: 'maven-publish'

repositories {
    mavenCentral()
}

group = 'com.ullink.parallel'
version = '0.9.1'
description 'Ordered Scheduler brings a lightweigth solution for unlocking code that is only synchronized because of ordered/sequential requirements'

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }
}

bintray {
    // to remove when upgraded plugindev to 1.0.4
    user project.properties.bintrayUser
    key project.properties.bintrayApiKey
    publications = ['mavenJava']
    publish = true
    pkg {
        userOrg = 'ullink'
        repo = 'maven'
        name = project.name
        desc = description
        labels = ['java', 'scheduler']
        licenses = ['Apache-2.0']
        websiteUrl = "https://github.com/Ullink/${project.name}"
        issueTrackerUrl = "https://github.com/Ullink/${project.name}/issues"
        vcsUrl = "https://github.com/Ullink/${project.name}"
        version.gpg.sign = true
    }
}

dependencies {
    testCompile(
            [group: 'junit', name: 'junit', version: '4.11'],
    )
}
